{
  description = "NixOS based syzkaller setup";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixpkgs-unstable";

    nixos-generators = {
      url = "github:nix-community/nixos-generators";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { self, nixpkgs, nixos-generators, ... }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs { inherit system; };
      lib = nixpkgs.lib;
      linux = pkgs.linuxPackages_testing;
    in {
      vm = nixos-generators.nixosGenerate {
        system = "x86_64-linux";
        modules = [ (import ./vm.nix { inherit pkgs lib linux; }) ];
        format = "raw";
      };

      devShells.${system}.default = pkgs.mkShell {
        nativeBuildInputs = with pkgs; [
          nixfmt
          syzkaller
          clang-tools
          nil
          qemu
          (writeShellScriptBin "build-vm" ''
            if [ ! -e "./syzkaller_ssh" ]; then
              ssh-keygen -N "" -C "syzkaller" -f ./syzkaller_ssh
              cat ./syzkaller_ssh.pub > ./authorized_keys
              git add authorized_keys
            fi

            nix build .#vm
            git reset authorized_keys
          '')
          (writeShellScriptBin "debug-vm" ''
            ${qemu}/bin/qemu-system-x86_64 -m 2048 -smp 2 -enable-kvm -cpu host,migratable=off -hda ./nixos.img
          '')
          (writeShellScriptBin "run-syzkaller" ''
            if [ ! -e "result" ]; then
              build-vm
            fi

            cp result/nixos.img .
            chmod 644 nixos.img

            cat <<-EOF > ./.syzkaller.conf
            # This is auto-generated by run-syzkaller! Changes will be overwritten. See flake.nix
            {
              "name": "QEMU-amd64",
              "target": "linux/amd64",
              "http": "127.0.0.1:56700",
              "workdir": "/tmp/syzkaller-workdir",
              "syzkaller": "${syzkaller}",
              "kernel_obj": "${linux.kernel.dev}",
              "kernel_src": "${linux.kernel.dev}",
              "image": "./nixos.img",
              "sshkey": "./syzkaller_ssh",
              "procs": 8,
              "type": "qemu",
              "vm": {
                "count": 4,
                "qemu": "${qemu}/bin/qemu-system-x86_64",
                "cpu": 2,
                "mem": 2048
              }
            }
            EOF

            syz-manager -config=./.syzkaller.conf "$@"
          '')
        ];
      };
    };
}
